_SECTION_BEGIN("Price");
SetChartOptions(0,chartShowArrows|chartShowDates);
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) Vol " +WriteVal( V, 1.0 ) +" {{VALUES}}", O, H, L, C, SelectedValue( ROC( C, 1 )) ));
Plot( C, "Close", ParamColor("Color", colorBlack ), styleNoTitle | styleCandle); 
if( ParamToggle("Tooltip shows", "All Values|Only Prices" ) )
{
 ToolTip=StrFormat("Open: %g\nHigh:  %g\nLow:   %g\nClose:  %g (%.1f%%)\nVolume: "+NumToStr( V, 1 ), O, H, L, C, SelectedValue( ROC( C, 1 )));
}
_SECTION_END();

_SECTION_BEGIN("Itrend");
SetBarsRequired(20, 0);
//Initial Formulas

// Ehlers ITrend
// from Ehlers, John F. Cybernetic Analysis for Stocks and Futures. Wiley. 2004. 
// Chapter 3, p. 21. Code on p. 24.

function ITrend(array, alpha)
{
  // initialize early array values and declare as array
  it = array;
  //it = (array[2] - 2*array[1] + array[0])/4; This initialization takes a long time to converge.
  for(i = 2; i < BarCount; i++)
  {
    it[i] = (alpha - alpha*alpha/4)*array[i] + 
         .5*alpha*alpha*array[i-1] -
         (alpha - .75*alpha*alpha)*array[i-2] +
         2*(1 - alpha)*it[i-1] -
         (1 - alpha)*(1 - alpha)*it[i-2];
  }
  return it;
}

P = (H+L)/2;
trend1 = ITrend(P, .15);
trend2 = Ref(trend1, -1);

//Entry Rules
//Buy = (P > trend OR Cross(P, trend)) ;
//Sell = (trend > P  OR Cross(trend, P));

Buy = Cross(trend1, trend2);
Sell = Cross(trend2, trend1); 

Buy = ExRem( Buy, Sell );
Sell = ExRem( Sell, Buy );

Short = Sell;
Cover = Buy;

//Plots
Plot(trend1, "ITrend1", colorRed, styleLine);
Plot(trend2, "ITrend2", colorBlue, styleLine);

PlotShapes( Sell  * shapeDownArrow, colorRed );
PlotShapes( Buy  * shapeUpArrow, colorGreen );