_SECTION_BEGIN("S_RVI");
SetBarsRequired(10000,10000);

#include <LRSI.afl>
#include <ITrend.afl>

// CyclePeriod
Med = (H+L)/2;
pds = sbDC(Med);

//Rules
//RVI

RVI = sbRVI(EndValue(pds));
Trigger = Ref(RVI,-1);

myBuy = Cross(RVI, Trigger);
myShort  =Cross( Trigger, RVI);

Buy =  ExRem(myBuy, myShort);
Short = ExRem(myShort, myBuy);

stoplong = scAtrBandLong(H,1.6*ATR(EndValue(pds)));
stopshort = scAtrBandShort(L,1.6*ATR(EndValue(pds)));

Cover = ExRem(Cross(C,stopshort),myShort) ;
Sell = ExRem(Cross(stoplong,C),myBuy);

SetChartOptions(0,chartShowArrows|chartShowDates);
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) {{VALUES}}", O, H, L, C, SelectedValue( ROC( C, 1 ) ) ));
Plot( C, "Close", ParamColor("Color", colorBlack ), styleNoTitle | ParamStyle("Style") | GetPriceStyle() ); 
Plot(stoplong, "", 3, 1);
Plot(stopshort, "", 9, 1);

PlotShapes( Buy * shapeUpArrow, colorGreen );
PlotShapes( Short * shapeDownArrow, colorRed );
PlotShapes( Sell  * shapeCircle, 3 );
PlotShapes( Cover * shapeCircle, 9, 0, Graph0, 12 );

//System Tests
SetTradeDelays(0,0,0,0);
BuyPrice = C;
SellPrice = C;
// Set the number of positions.
NumberPositions = 10;
SetOption("MaxOpenPositions", NumberPositions);
// Allocate available funds equally among them.
PositionSize = -100 / NumberPositions;
Lookback = 5;
PositionScore = -ROC(C,Lookback);
_SECTION_END();