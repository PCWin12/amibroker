// IssueSelectionFilter1.afl
//
// This AmiBroker program uses filters to help
// with issue selection.
//
// To use this filter,
// 1. Analysis >> Automatic Analysis
// 2. “Pick”, then select this file, and Open it
// 3. “Explore”
//
// With the results of any exploration,
// you can sort the result of any column
// by clicking on that column’s header.
//
// Assume we are working with stocks and using daily bars.
// There are about 252 trading days in a year.
// See if there is a closing price for the date 10 years ago.
PriceYearsAgo = Ref(C,-2520);
HistoryExists  = PriceYearsAgo > 0;
// Find the lowest closing price for the past 10 years.
LowestClose = LLV(C,2520);
PriceReasonable = LowestClose > 2.00;
// Compute the average daily Liquidity
// (Closing Price times Volume) for the past Year.
Liquidity = MA(C*V,252);
LiquidityOK = Liquidity > 1000000;
// “Comment Out” the “Filter = “ statement that
// you do not want to use,
// leaving the other active.
// Allow all issues to pass the filter.
Filter = 1;
// Set the filter to block issues that do not
// meet our criteria.
// Filter = HistoryExists AND PriceReasonable AND LiquidityOK;
// Add columns to report the items of interest.
AddColumn(C,"Close",4.2);
AddColumn(PriceYearsAgo,"PriceYearsAgo",4.2);
AddColumn(LowestClose,"LowestClose",4.2);
AddColumn(Liquidity,"Liquidity",4.0);