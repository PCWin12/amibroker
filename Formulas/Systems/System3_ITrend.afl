_SECTION_BEGIN("Price");
SetChartOptions(0,chartShowArrows|chartShowDates);
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) Vol " +WriteVal( V, 1.0 ) +" {{VALUES}}", O, H, L, C, SelectedValue( ROC( C, 1 )) ));
Plot( C, "Close", ParamColor("Color", colorBlack ), styleNoTitle | styleCandle); 
if( ParamToggle("Tooltip shows", "All Values|Only Prices" ) )
{
 ToolTip=StrFormat("Open: %g\nHigh:  %g\nLow:   %g\nClose:  %g (%.1f%%)\nVolume: "+NumToStr( V, 1 ), O, H, L, C, SelectedValue( ROC( C, 1 )));
}
_SECTION_END();

_SECTION_BEGIN("Itrend");
SetBarsRequired(20, 0);
//Initial Formulas

// Ehlers ITrend
// from Ehlers, John F. Cybernetic Analysis for Stocks and Futures. Wiley. 2004. 
// Chapter 3, p. 21. Code on p. 24.

function ITrend(array, alpha)
{
  // initialize early array values and declare as array
  it = array;
  //it = (array[2] - 2*array[1] + array[0])/4; This initialization takes a long time to converge.
  for(i = 2; i < BarCount; i++)
  {
    it[i] = (alpha - alpha*alpha/4)*array[i] + 
         .5*alpha*alpha*array[i-1] -
         (alpha - .75*alpha*alpha)*array[i-2] +
         2*(1 - alpha)*it[i-1] -
         (1 - alpha)*(1 - alpha)*it[i-2];
  }
  return it;
}

P = (H+L)/2;
trend1 = ITrend(P, .15);
trend2 = Ref(trend1, -1);


P = (H+L)/2;

A2 = WMA(P, 21);
A3	= WMA(P, 60);

Bull = A2>A3;
Bear = A3>A2;

Buy 	= (Cross(trend1, trend2) AND Bull) OR Cross(A2, A3);
Cover 	= Cross(trend1, trend2) ;

Sell	= Cross(trend2, trend1) ;
Short	= (Cross(trend2, trend1) AND Bear) OR Cross (A3, A2);


//Plots
Plot(trend1, "ITrend1", colorRed, styleLine);
Plot(trend2, "ITrend2", colorBlue, styleLine);
Plot(A2, "WMA(21)", colorGreen, styleLine);
Plot(A3, "WMA(60)", colorDarkGreen, styleLine);

PlotShapes( Sell  * shapeDownArrow, colorRed );
PlotShapes( Buy  * shapeUpArrow, colorGreen );
PlotShapes( Short  * shapeHollowDownArrow, colorRed );
PlotShapes( Cover  * shapeHollowUpArrow, colorGreen );