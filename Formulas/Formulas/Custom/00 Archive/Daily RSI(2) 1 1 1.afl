_SECTION_BEGIN("Daily RSI");
//Variables
P 		= (O+H+L+C)/4;
X1 		= RSIa(P,2);
X2 		= RSIa(P,5);

wc = TimeFrameCompress( P, inWeekly ); 

/* now the time frame is still unchanged (say daily) and our MA will operate on daily data */ 
dailyma = EMA( P, 200 ); 

/* but if we call MA on compressed array, it will give MA from other time frame */ 
weeklyma = EMA( wc, 40 ); // note that argument is time-compressed array 
weeklyma = TimeFrameExpand( weeklyma, inWeekly ); // expand for display 

Plot( dailyma, "DailyMA", colorRed ); 
Plot( weeklyma, "WeeklyMA", colorBlue ); 

A1 		= ATR(2);
C1 		= 95; 
C2 		= 90;
C3 		= 3;

//Long Rules
B1 		= Cross(100-C1,X1);
B2 		= Cross(100-C2,X1);

P_B1 	= ValueWhen(B1, P);
A1_B1 	= ValueWhen(B1, A1);

B3 = L < (P_B1 - A1_B1 / 2) AND BarsSince(B1) < C3;

//Exit Rules Long
Selllevel = ValueWhen(B3, L) - A1 * 2;
Sell1 = Cross(X1, 70) OR Cross(Selllevel, P) OR BarsSince(B3) > 5;

//Short Rules
S1 = Cross(X1,C1);
S2 = Cross(X1,C2);

P_S1 	= ValueWhen(S1, P);
A1_S1 	= ValueWhen(S1, A1);

S3 = H > (P_S1 + A1_S1 / 2) AND BarsSince(S1) < C3;

//Exit Rules Short
Coverlevel = ValueWhen(S3, H) + A1 * 2;
Cover1 = Cross(30, X1) OR Cross(P, Coverlevel) OR BarsSince(S3) > 5;

//Position Sizing
SetPositionSize( IIf(B3 OR S3, 50, 50), spsPercentOfEquity);
//SetPositionSize(1, spsShares);

//Entry Conditions
Buy = B3;
Short = S3;

//Exit Conditions
Sell = Sell1;
Cover = Cover1;

//Signal Cleanup
Buy = ExRem(Buy, Sell);
Sell = ExRem(Sell, Buy);

Short = ExRem(Short, Cover);
Cover = ExRem(Cover, Short);

//Display to GUI
_N(Title = StrFormat("      {{NAME}} - {{INTERVAL}} {{DATE}}\n"
+ "B1: %g  B2: %g B3:%g     S1: %g  S2: %g  S3: %g     A1: %g\n"
+ "P_B1: %g A1_B1: %g Coverlevel: %g Selllevel: %g"
+ "\n P %g, Open %g High %g Low %g Close %g (%.1f%%) \n"
  + WriteVal( V, 1.0 ), B1, B2, B3, S1, S2, S3, A1, P_B1, A1_B1, Coverlevel, Selllevel, P, O, H, L, C) );

Plot(C, "Price", colorBlack, styleCandle);

PlotShapes(Buy * (shapeCircle), colorGold, 0, C - A1);
PlotShapes(Sell * (shapeHollowCircle), colorOrange);
PlotShapes(Cover * (shapeHollowCircle), colorGold);
PlotShapes(Short * (shapeCircle), colorOrange, 0, C + A1);

//TradeType = Flip(Buy, Short);
//SetChartBkColor(LastValue(IIf( TradeType>0, colorDarkGreen, colorDarkRed)));

//Scan Rules