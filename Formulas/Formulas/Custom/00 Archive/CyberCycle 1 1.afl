_SECTION_BEGIN("CyberCycle");
SetBarsRequired( 200, 0 );

// Ehlers CyberCycle
// Cybernetic Analysis for Stocks and Futures
// Chapter 4, p. 33. Code on p. 38.
// Original code is at:
// http://www.traders.com/Documentation/FEEDbk_docs/Archive/052004/TradersTips/TradersTips.html#amibroker

function CyberCycle( array, alpha )
{
  smooth = ( array + 2 * Ref( array, -1 ) +
             2 * Ref( array, -2 ) + Ref( array, -3 ) ) / 6;
  // init value
  Cycle = ( array[ 2 ] - 2 * array[ 1 ] + array[ 0 ] )/4;
  for( i = 6; i < BarCount; i++ )
  {
     Cycle[ i ] = ( ( 1 - 0.5 * alpha) ^ 2 ) *
                  ( smooth[ i ] - 2 * smooth[ i - 1 ] + smooth[ i - 2] ) +
                  2 * ( 1 - alpha ) * Cycle[ i - 1 ] -
                  ( ( 1 - alpha) ^ 2 ) * Cycle[ i - 2 ];
  }
  return Cycle;
}

P = (H+L)/2;
C1 = SelectedValue( sbDC(P));//Optimize("C1", 20, 2, 20, 1);//19
Cycle = CyberCycle(P , 1/C1 );
Trigger = Ref(cycle, -1);
Plot( Cycle, "CyberCycle", colorBlue );
Plot( Trigger, "Trigger", colorRed);

Short = Cross(Trigger, Cycle);
Buy  = Cross(Cycle, Trigger);

PlotShapes( Buy * shapeUpArrow, colorGreen );
PlotShapes( Short * shapeDownArrow, colorRed );
_SECTION_END();